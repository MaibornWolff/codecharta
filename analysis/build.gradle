buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlin_version
    }
}

plugins {
    id 'org.sonarqube' version '2.7.1'
    id 'distribution'
}

allprojects {
    group 'de.maibornwolff.codecharta'
    version = currentVersion

    repositories.addAll(rootProject.getBuildscript().getRepositories())

    apply plugin: 'jacoco'
    apply plugin: 'maven'

    jacoco {
        toolVersion = jacoco_version
        reportsDir = file("$buildDir/reports")
    }
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectKey", "de.maibornwolff.codecharta:analysis"
        property "sonar.projectName", "CodeCharta Analysis"
        property "sonar.sourceEncoding", "UTF-8"
    }
}

subprojects {
    apply plugin: 'kotlin'

    dependencies {
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version

        testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
        testCompile("org.junit.jupiter:junit-jupiter-api:$junit5_version")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junit5_version")
        testCompile("org.junit.jupiter:junit-jupiter-params:$junit5_version")
        testImplementation "com.natpryce:hamkrest:$hamkrest_version"
    }

    compileJava {
        options.compilerArgs += ['--release', '8']
    }

    compileTestJava {
        options.compilerArgs += ['--release', '8']
    }

    compileKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }
}

distributions {
    main {
        baseName = 'codecharta-analysis'
        contents {
            from { '../LICENSE.md' }
            from { '../CHANGELOG.md' }

            // Readmes
            from { 'README.md' }

            project.subprojects.each { p ->
                p.plugins.withType(ApplicationPlugin) {
                    into('bin') {
                        from { p.startScripts.outputs.files }
                        fileMode = 0755
                    }
                    into('lib') {
                        from { p.jar }
                    }
                }
            }

            // deprecated ccsh
            from { 'dist/ccsh' }
        }
    }
}

task integrationTest(type: Exec) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        println "In order to run the integration tests a bash script is executed."
        println "Make sure to use a shell with bash capability (e.g. GitBash) to run this task."
    }

    executable 'sh'
    workingDir('test')
    args '-c', "./golden_test.sh $version"
}

integrationTest.dependsOn build
