buildscript {
    ext {
        // Dependency versions
        boon_version = '0.34'
        commons_lang3_version = '3.11'
        gson_version = '2.6'
        jackson_version = '2.11.2'
        jacoco_version = '0.8.5'
        jaxb_api_version = '2.3.1'
        jersey_version = '2.31'
        json_schema_version = '1.12.1'
        json_version = '20190722'
        juniversalchardet_version = '1.0.3'
        kotlin_logging_version = '1.8.3'
        kotlin_version = '1.4.0'
        kotlinx_coroutines_version = '1.3.9'
        picocli_version = '4.5.1'
        slf4j_version = '1.7.30'
        sonar_java_version = '5.14.0.18788'
        sonarqube_version = '3.0'
        univocity_parsers_version = '2.9.0'

        // Test dependency versions
        assertj_version = '3.17.0'
        hamcrest_version = '1.3'
        hamkrest_version = '1.7.0.3'
        junit4_version = '4.12'
        junit5_version = '5.6.2'
        junit_platform_version = '1.6.2'
        ktlint_version = '9.4.0'
        mockito_core_version = '3.5.7'
        mockito_kotlin_version = '2.2.0'
        mokk_version = '1.10.0'
        spek2_version = '2.0.2'
        wiremock_version = '2.19.0'
    }

    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        maven {
            url 'https://jitpack.io'
        }
    }
    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlin_version
    }
}

plugins {
    id 'org.sonarqube' version "${sonarqube_version}"
    id 'distribution'
    id "org.jlleitschuh.gradle.ktlint" version "${ktlint_version}"
}

allprojects {
    group 'de.maibornwolff.codecharta'
    version = currentVersion

    repositories.addAll(rootProject.getBuildscript().getRepositories())

    apply plugin: 'jacoco'
    apply plugin: 'maven'
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    jacoco {
        toolVersion = jacoco_version
        reportsDir = file("$buildDir/reports")
    }
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectKey", "maibornwolff-gmbh_codecharta_analysis"
        property "sonar.projectName", "CodeCharta Analysis"
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-kapt'

    dependencies {
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
        implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: kotlinx_coroutines_version

        testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
        testCompile("org.junit.jupiter:junit-jupiter-api:$junit5_version")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junit5_version")
        testCompile("org.junit.jupiter:junit-jupiter-params:$junit5_version")
        testImplementation "com.natpryce:hamkrest:$hamkrest_version"

        kapt group: 'info.picocli', name: 'picocli-codegen', version: picocli_version
    }

    compileJava {
        options.compilerArgs += ['--release', '8']
    }

    compileTestJava {
        options.compilerArgs += ['--release', '8']
    }

    compileKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }
    kapt {
        arguments {
            arg("project", "${project.group}/${project.name}")
        }
    }
}

distributions {
    main {
        baseName = 'codecharta-analysis'
        contents {
            from { '../LICENSE.md' }
            from { '../CHANGELOG.md' }

            // Readmes
            from { 'README.md' }

            project.subprojects.each { p ->
                p.plugins.withType(ApplicationPlugin) {
                    into('bin') {
                        from { p.startScripts.outputs.files }
                        fileMode = 0755
                    }
                    into('lib') {
                        from { p.jar }
                    }
                }
            }

            // deprecated ccsh
            from { 'dist/ccsh' }
        }
    }
}

task integrationTest(type: Exec) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        println "In order to run the integration tests a bash script is executed."
        println "Make sure to use a shell with bash capability (e.g. GitBash) to run this task."
    }

    executable 'sh'
    workingDir('test')
    args '-c', "./golden_test.sh $version"
}

integrationTest.dependsOn build
